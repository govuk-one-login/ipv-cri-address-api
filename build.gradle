plugins {
	alias(libs.plugins.java)
	alias(libs.plugins.spotless)
	alias(libs.plugins.sonarqube)
	alias(libs.plugins.jacoco)
}

defaultTasks 'clean', 'spotlessApply', 'build'

repositories {
	maven {
		url 'https://gds.jfrog.io/artifactory/di-allowed-repos'
	}
}

subprojects {
	apply plugin: 'java'

	java {
		sourceCompatibility = JavaVersion.VERSION_11
		targetCompatibility = JavaVersion.VERSION_11
	}

	repositories {
		maven {
			url 'https://gds.jfrog.io/artifactory/di-allowed-repos'
		}
	}

	// The dynamodb enhanced package loads the apache-client as well as the spi-client, so
	// we need to add the apache-client to the dependencies exclusion to not get a mismatch
	configurations.configureEach {
		exclude group: "software.amazon.awssdk", module: "apache-client"
	}

	dependencies {
		implementation platform("software.amazon.awssdk:bom:2.26.20")
		implementation platform("com.fasterxml.jackson:jackson-bom:2.18.3")
		implementation(libs.bundles.otel)
	}

	tasks.register("pactTests", Test) {
		useJUnitPlatform {
			includeTags 'Pact'
		}
	}

	test {
		useJUnitPlatform()
		finalizedBy(tasks.matching { it.name == 'jacocoTestReport' })
	}

	tasks.matching { it.name == "jacocoTestReport" }.configureEach {
		dependsOn test
		reports {
			xml.required.set(true)
		}
	}
}

sonar {
	properties {
		property "sonar.projectName", "ipv-cri-address-api-java"
		property "sonar.projectKey", "ipv-cri-address-api-java"
		property "sonar.organization", "govuk-one-login"
		property "sonar.host.url", "https://sonarcloud.io"
	}
}

spotless {
	java {
		target "**/src/**/*.java"
		googleJavaFormat("1.13.0").aosp()
		importOrder "", "javax", "java", "\\#"
		endWithNewline()
	}
	groovyGradle {
		target '**/*.gradle'
		greclipse()
		trimTrailingWhitespace()
		endWithNewline()
	}
}

clean.doFirst {
	delete "${rootDir}/dist/"
	delete "${rootDir}/.aws-sam"
}

gradle.projectsEvaluated {
	tasks.withType(JavaCompile).tap {
		configureEach {
			options.compilerArgs << "-Xlint:unchecked"
		}
	}
}
